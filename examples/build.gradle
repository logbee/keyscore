project.apply plugin: 'maven-publish'

task build {
    group("Build")
}

task clean(type: Delete) {
    group("Build")
    delete buildDir
}

projectDir.listFiles().findAll { it.isDirectory() && it.name.contains("example") }.each { exampleDir ->

    def example = exampleDir.name
    def taskName = "zip${example.split('-').inject("", { result, part -> result + part.substring(0, 1).toUpperCase() + part.substring(1) })}"

    def zipTask = tasks.create(taskName, Zip.class, { task ->
        task.group("Build")
        task.description("Creates a zip archive from '$example'.")
        task.archiveName "$example-${rootProject.version}.zip"
        task.destinationDir new File(buildDir, "archives")
        task.from exampleDir
        task.into example
    })

    publishing {
        publications {
            "$example"(MavenPublication) {
                groupId "${project.group}.examples"
                artifactId example
                version project.version
                artifact zipTask
            }
        }
    }

    tasks['build'].inputs.files(zipTask)
}
