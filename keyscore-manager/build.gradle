plugins {
    id "com.moowork.node" version "1.2.0"
}

import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerRemoveContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer
import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

node {
    version = '8.9.4'
    download = true
}

npmInstall {
    args = ['--verbose', '--loglevel', 'verbose']
}

task webpack(type: NodeTask, dependsOn: npmInstall) {
    script = file('node_modules/.bin/webpack')
    inputs.files([
        file('node_modules/.bin/webpack'),
        files("src")
    ])
}

task serve(type: NodeTask, dependsOn: npmInstall) {
    script = file('node_modules/.bin/webpack-dev-server')
}

task createDockerfile(type: Dockerfile) {
    destFile = project.file("$buildDir/tmp/Dockerfile")
    from 'nginx:1.13.7-alpine'
    instruction { 'COPY public/ build/webpack/ /usr/share/nginx/html/' }
    instruction { 'COPY conf/nginx.conf /etc/nginx/nginx.conf' }
    instruction { 'COPY conf/application.conf /usr/share/nginx/html/' }
    instruction { 'COPY build/resources/keyscore.light.svg /usr/share/nginx/html/' }
    instruction { "CMD nginx -g 'daemon off;'" }
    instruction { 'EXPOSE 80' }
}

task copyResources(type: Copy) {
    from file('../media/keyscore.light.svg')
    into "$buildDir/resources/"
}

task buildDockerImage(type: DockerBuildImage, dependsOn: [createDockerfile, copyResources, webpack]) {
    inputDir = projectDir
    dockerFile = createDockerfile.destFile
    tag = 'logbee/keyscore-manager:latest'
}

task createDockerContainer(type: DockerCreateContainer) {
    dependsOn buildDockerImage
    containerName = project.name
    targetImageId { buildDockerImage.getImageId() }
    portBindings = ['8080:80']
}

task startDockerContainer(type: DockerStartContainer) {
    dependsOn createDockerContainer
    targetContainerId { createDockerContainer.containerName }
}

task stopDockerContainer(type: DockerStopContainer) {
    targetContainerId { createDockerContainer.containerName }
}

task removeDockerContainer(type: DockerRemoveContainer) {
    dependsOn stopDockerContainer
    targetContainerId { createDockerContainer.containerName }
}

task build(dependsOn: webpack) {
}

task clean(type: Delete) {
    delete 'build'
}

task cleanAll(type: Delete, dependsOn: clean) {
    delete 'node_modules'
}
