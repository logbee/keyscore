import io.logbee.gradle.scalapb.ScalaPBPlugin

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: ScalaPBPlugin

apply from: rootProject.file('dependencies.gradle')

repositories {
    mavenCentral()
}

dependencies {

    compile dependency.scala_lang
    compile dependency.json4s
    compile dependency.protobuf_java
    compile dependency.scalapb_runtime

    testCompile dependency.scala_test
    testCompile dependency.junit
}

bintray {
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = project.bintrayUserOrg
        licenses = ['Apache-2.0']
        vcsUrl = project.scmUrl
        version {
            name = project.name
            desc = project.description
            released  = new Date()
            vcsTag = project.version
        }
    }
    publications = ['keyscoreModel']
}

publishing {
    publications {
        keyscoreModel(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java
            artifact sourcesJar
            artifact scaladocJar
            pom.withXml {
                buildPomXml(project, asNode())
            }
        }
    }
}

test {
    testLogging {
        events 'started', 'passed', 'skipped', 'failed'
        exceptionFormat "full"
    }
}

jar {
    manifest.attributes ([
        'Implementation-Title': project.name,
        'Implementation-Version': version,
        'Implementation-Revision' : revision,
        'Implementation-Vendor'   : 'logbee.io'
    ])
    from (rootProject.projectDir) {
        include 'LICENSE'
    }
    into("META-INF/maven/$project.group/$project.name") {
        from generatePomXml
    }
}
