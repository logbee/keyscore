import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerRemoveContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

apply from: rootProject.file('dependencies.gradle')

repositories {
    mavenCentral()
}

project.ext {
    distributionDir = "$buildDir/distribution"
    dockerImageName = "$dockerImageHost/$dockerImageNamespace/${project.name}"
}

configurations {
    testOutput
}

dependencies {

    compile project(':keyscore-commons')
    compile project(':keyscore-model')

    compile project(':keyscore-pipeline:keyscore-pipeline-api')

    compile project(':keyscore-agent:keyscore-agent-runtimes-api')
    compile project(':keyscore-agent:keyscore-agent-runtimes-jvm')

    compile dependency.scala_lang
    compile dependency.akka_actor
    compile dependency.akka_persistence
    compile dependency.akka_stream
    compile dependency.akka_slf4j
    compile dependency.akka_http
    compile dependency.akka_http_json
    compile dependency.akka_cluster

    runtime project(':keyscore-commons:keyscore-commons-logging')
    runtime project(':keyscore-pipeline:keyscore-pipeline-commons')
    runtime project(':keyscore-pipeline:keyscore-pipeline-contrib')

    runtime dependency.logback

    testCompile project(':keyscore-test-fixtures')
    testCompile project(':keyscore-pipeline:keyscore-pipeline-contrib')
    testCompile project(':keyscore-frontier')

    testCompile "com.dimafeng:testcontainers-scala_2.13:0.30.0"

    testCompile dependency.scala_test
    testCompile dependency.akka_testkit

    testCompile dependency.junit

    testOutput sourceSets.main.output
    testOutput sourceSets.test.output
}

apply plugin: 'nebula.ospackage'

jar {
    exclude('**/.keep')
    manifest.attributes([
            'Main-Class'                  : mainClass,
            'Implementation-Title'        : project.name,
            'Implementation-Version'      : version,
            'Implementation-Revision'     : revision,
            'Implementation-Revision-Date': revisionDate,
            'Implementation-Build-Date'   : buildDate,
            'Implementation-Vendor'       : 'logbee.io',
            'Class-Path'                  : (configurations.runtime + configurations.compile).collect {
                "libs/" + it.getName()
            }.join(' ')
    ])
}

distributions {
    main {
        contents {
            from('src/launcher/keyscore-agent')
            from(jar.outputs) {
                rename("${project.name}-${version}.jar", "${project.name}.jar")
            }
            from(configurations.runtime) {
                into 'libs'
            }
        }
    }
}

ext.compv = { p1 ->
    def result = version

    switch (p1) {
        case "deb":
            break
        case "rpm":
            // ugly
            result = result.replace("-alpha.", ".a").replace("-beta.", ".b")
            break
    }

    return result
}

/**
 * Creates buildDeb and buildRpm
 */
ospackage {
    packageName = 'keyscore-agent'
    packageDescription = 'The mighty Agent of Keyscore'
    maintainer = 'mlandth'
    vendor = 'logbee'
    url = 'https://github.com/logbee/keyscore'

    os = 'LINUX' // only applied to RPM

    //scripts
    postInstall file('src/scripts/debian_post_install.sh')
    preUninstall file('src/scripts/debian_pre_removal.sh')

    //install location
    into '/usr/share/keyscore/keyscore-agent'

    //source files
    from(project.tasks['installDist']) {
        into 'bin'
    }

    from('src/scripts') {
        into 'scripts'
    }

    from("$project.gradle.gradleUserHomeDir/jdk")
}

buildDeb {
    dependsOn {
        [':unpackJDK', project.tasks['installDist']]
    }
    version = compv("deb")
    release = project.findProperty('debRelease')
    arch = 'amd64' //default = all
}

buildRpm {
    dependsOn {
        [':unpackJDK', project.tasks['installDist']]
    }
    version = compv("rpm")
    release = project.findProperty('rpmRelease')
    epoch = 0 //default = 0
    arch = 'X86_64' //? default = NOARCH
}

task buildDockerfile(type: Dockerfile, dependsOn: 'buildDeb') {
    destFile = project.file("$buildDir/tmp/Dockerfile")
    from 'quay.io/logbee/docker-buster-slim:latest'
    def deb = "keyscore-agent_${compv("deb")}-${project.findProperty('debRelease')}_amd64.deb"
    instruction { "COPY build/distributions/$deb /tmp/" }
    instruction { "RUN /usr/bin/dpkg -i /tmp/$deb && rm /tmp/$deb" }
    instruction { "CMD /usr/share/keyscore/keyscore-agent/bin/keyscore-agent" }
}

task buildRaspbianDockerfile(type: Dockerfile) {
    destFile = project.file("$buildDir/tmp/RaspbianDockerfile")
    from 'balenalib/raspberrypi3-alpine-openjdk' //TODO alpine?
    instruction { "RUN [ \"cross-build-start\" ]" }
    instruction { "COPY build/install/ /opt/" }
    instruction { "CMD /opt/keyscore-agent/keyscore-agent" }
    instruction { "RUN [ \"cross-build-end\" ]" }
}

["", "Raspbian"].each { arch ->
    def dockerfileTask = tasks["build${arch}Dockerfile"]
    task "build${arch}DockerImage"(type: DockerBuildImage, dependsOn: [installDist, dockerfileTask]) {
        inputDir = projectDir
        dockerFile = dockerfileTask.destFile
        if (arch.isEmpty()) {
            tag = "$dockerImageName:$version"
        } else {
            tag = "$dockerImageName:${arch.toLowerCase()}-$version"
        }
    }
}

task createDockerContainer(type: DockerCreateContainer) {
    dependsOn buildDockerImage
    containerName = project.name
    targetImageId { buildDockerImage.getImageId() }
    portBindings = ['3551:3551']
    hostName = project.name
    env = [
            'KEYSCORE_CLUSTER_SEED_NODE_HOST=keyscore-frontier',
            'KEYSCORE_REMOTE_HOST=keyscore-agent',
            'KEYSCORE_REMOTE_PORT=0',
            'KEYSCORE_REMOTE_BIND_HOST=0.0.0.0'
    ]
    network = project.findProperty('containerNetwork') ?: 'host'
}

task startDockerContainer(type: DockerStartContainer) {
    dependsOn = ['createDockerContainer', ':setupDockerNetwork']
    targetContainerId { createDockerContainer.containerName }
}

task stopDockerContainer(type: DockerStopContainer) {
    targetContainerId { createDockerContainer.containerName }
    onError { exception ->
        if (exception.class.simpleName.matches('^(NotModified|NotFound)Exception$')) {
            println("Container " + createDockerContainer.containerName + " has already been stopped")
        } else throw exception
    }

}

task removeDockerContainer(type: DockerRemoveContainer) {
    dependsOn stopDockerContainer
    targetContainerId { createDockerContainer.containerName }
    onError { exception ->
        if (exception.class.simpleName.matches('^(NotModified|NotFound)Exception$')) {
            println("Container " + createDockerContainer.containerName + " has already been removed")
        } else throw exception
    }

}

task stopDockerContainerOnStartup(type: DockerStopContainer) {
    targetContainerId { createDockerContainer.containerName }
    onError { exception ->
        if (exception.class.simpleName.matches('^(NotModified|NotFound)Exception$')) {
            println("Container " + createDockerContainer.containerName + " has already been stopped")
        } else throw exception
    }

}

task removeDockerContainerOnStartup(type: DockerRemoveContainer) {
    dependsOn stopDockerContainerOnStartup
    targetContainerId { createDockerContainer.containerName }
    onError { exception ->
        if (exception.class.simpleName.matches('^(NotModified|NotFound)Exception$')) {
            println("Container " + createDockerContainer.containerName + " has already been removed")
        } else throw exception
    }

}

task pushDockerImage(type: DockerPushImage) {
    dependsOn buildDockerImage
    imageName = dockerImageName
    tag = version
}