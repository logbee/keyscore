import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerRemoveContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import org.gradle.api.internal.artifacts.dependencies.DefaultExternalModuleDependency
import org.gradle.api.internal.artifacts.dependencies.DefaultProjectDependency

apply from: rootProject.file('dependencies.gradle')

repositories {
    mavenCentral()
}

project.ext {
    distributionDir = "$buildDir/distribution"
    dockerImageName = "$dockerImageHost/$dockerImageNamespace/${project.name}"
}

configurations {
    testOutput
    bundle
    bundleProject
    bundleProject.transitive = false
}

dependencies {

    compile project(':keyscore-commons')
    compile project(':keyscore-model')

    compile project(':keyscore-pipeline:keyscore-pipeline-api')

    compile dependency.scala_lang
    compile dependency.akka_actor
    compile dependency.akka_persistence
    compile dependency.akka_stream
    compile dependency.akka_slf4j
    compile dependency.akka_http
    compile dependency.akka_http_json
    compile dependency.akka_cluster

    compile dependency.osgi
    compile dependency.felix

//    bundle dependency.osgi_compendium
    bundle dependency.slf4j
//    bundle dependency.logback
//    bundle dependency.felix_cmpn
//    bundle dependency.pax_assembly

    bundle project(':keyscore-agent:keyscore-agent-core')

    runtime project(':keyscore-pipeline:keyscore-pipeline-contrib')

    runtime dependency.logback

    testCompile project(':keyscore-test-fixtures')
    testCompile project(':keyscore-pipeline:keyscore-pipeline-contrib')
    testCompile project(':keyscore-frontier')

    testCompile dependency.scala_test
    testCompile dependency.akka_testkit

    testCompile dependency.junit

    testOutput sourceSets.main.output
    testOutput sourceSets.test.output
}

test {
    testLogging {
        events 'started', 'passed', 'skipped', 'failed'
        exceptionFormat "full"
    }
}

jar {
    manifest.attributes([
        'Main-Class'              : mainClass,
        'Implementation-Title'    : project.name,
        'Implementation-Version'  : version,
        'Implementation-Revision' : revision,
        'Class-Path'              : (configurations.runtime + configurations.compile).collect {
                                      "libs/" + it.getName() 
                                    }.join(' ')
    ])
}

distributions {
    main {
        contents {
            from('src/launcher/keyscore-agent')
            from(jar.outputs) {
                rename("${project.name}-${version}.jar", "${project.name}.jar")
            }
            from(configurations.runtime) {
                into 'libs'
            }
        }
    }
}

task prepareBundles(type: Copy) {
    into '.bundles'
    from(configurations.bundle)

    doLast {
        configurations.bundle.dependencies.forEach({ dependency ->
            if (dependency instanceof DefaultProjectDependency) {
                println "project: " + dependency
                (dependency as DefaultProjectDependency).artifacts.forEach { artifact ->
                    println "artifact: " + artifact
                }
            }
            else if (dependency instanceof DefaultExternalModuleDependency) {
                println "external: " + dependency
                (dependency as DefaultExternalModuleDependency).artifacts.forEach { artifact -> 
                    println "artifact: " + artifact
                }
            }
        })
    }
}

task prepare(dependsOn: [prepareBundles]) {}

task buildDockerfile(type: Dockerfile) {
    destFile = project.file("$buildDir/tmp/Dockerfile")
    from 'quay.io/logbee/openjdk-8-jdk-alpine'
    instruction { "COPY build/install/ /opt/" }
    instruction { "CMD /opt/keyscore-agent/keyscore-agent" }
}

task buildRaspbianDockerfile(type: Dockerfile) {
    destFile = project.file("$buildDir/tmp/RaspbianDockerfile")
    from 'balenalib/raspberrypi3-alpine-openjdk' //TODO alpine?
    instruction { "RUN [ \"cross-build-start\" ]"}
    instruction { "COPY build/install/ /opt/" }
    instruction { "CMD /opt/keyscore-agent/keyscore-agent" }
    instruction { "RUN [ \"cross-build-end\" ]"}
}

["", "Raspbian"].each { arch ->
    def dockerfileTask = tasks["build${arch}Dockerfile"]
    task "build${arch}DockerImage"(type: DockerBuildImage, dependsOn: [installDist, dockerfileTask]) {
        inputDir = projectDir
        dockerFile = dockerfileTask.destFile
        if (arch.isEmpty()) {
            tag = "$dockerImageName:$version"
        }
        else {
            tag = "$dockerImageName:${arch.toLowerCase()}-$version"
        }
    }
}

task createDockerContainer(type: DockerCreateContainer) {
    dependsOn buildDockerImage
    containerName = project.name
    targetImageId { buildDockerImage.getImageId() }
    portBindings = ['3551:3551']
    hostName = project.name
    env = [
        'KEYSCORE_CLUSTER_SEED_NODE_HOST=keyscore-frontier',
        'KEYSCORE_REMOTE_HOST=keyscore-agent',
        'KEYSCORE_REMOTE_PORT=0',
        'KEYSCORE_REMOTE_BIND_HOST=0.0.0.0'
    ]
    network = project.findProperty('containerNetwork') ?: 'host'
}

task startDockerContainer(type: DockerStartContainer) {
    dependsOn = ['createDockerContainer',':setupDockerNetwork']
    targetContainerId { createDockerContainer.containerName }
}

task stopDockerContainer(type: DockerStopContainer) {
    targetContainerId { createDockerContainer.containerName }
    onError { exception ->
        if (exception.class.simpleName.matches('^(NotModified|NotFound)Exception$')) {
            println("Container " + createDockerContainer.containerName + " has already been stopped")
        } else throw exception
    }

}

task removeDockerContainer(type: DockerRemoveContainer) {
    dependsOn stopDockerContainer
    targetContainerId { createDockerContainer.containerName }
    onError { exception ->
        if (exception.class.simpleName.matches('^(NotModified|NotFound)Exception$')) {
            println("Container " + createDockerContainer.containerName + " has already been removed")
        } else throw exception
    }

}

task stopDockerContainerOnStartup(type: DockerStopContainer) {
    targetContainerId { createDockerContainer.containerName }
    onError { exception ->
        if (exception.class.simpleName.matches('^(NotModified|NotFound)Exception$')) {
            println("Container " + createDockerContainer.containerName + " has already been stopped")
        } else throw exception
    }

}

task removeDockerContainerOnStartup(type: DockerRemoveContainer) {
    dependsOn stopDockerContainerOnStartup
    targetContainerId { createDockerContainer.containerName }
    onError { exception ->
        if (exception.class.simpleName.matches('^(NotModified|NotFound)Exception$')) {
            println("Container " + createDockerContainer.containerName + " has already been removed")
        } else throw exception
    }

}

task pushDockerImage(type: DockerPushImage) {
    dependsOn buildDockerImage
    imageName = dockerImageName
    tag = version
}

task cleanAll(type: Delete, dependsOn: clean) {
    delete '.bundles', '.felix'
}