plugins {
    id "com.bmuschko.docker-remote-api" version "3.2.1"
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

apply plugin: 'scala'

repositories {
    mavenCentral()
}

project.ext {
    distributionDir = "$buildDir/distribution"
}

dependencies {

    compile project(':keyscore-model')

    compile 'org.scala-lang:scala-library:2.12.4'
    compile 'com.typesafe.akka:akka-actor_2.12:2.5.6'
    compile 'com.typesafe.akka:akka-persistence_2.12:2.5.6'
    compile 'com.typesafe.akka:akka-stream_2.12:2.5.6'
    compile 'com.typesafe.akka:akka-http_2.12:10.0.10'
    compile 'com.typesafe.akka:akka-http-spray-json_2.12:10.0.10'
    compile 'com.typesafe.akka:akka-persistence-cassandra_2.12:0.22'
    compile 'com.typesafe.akka:akka-slf4j_2.12:2.5.6'
    compile 'org.slf4j:slf4j-log4j12:1.7.25'
    compile 'com.typesafe.akka:akka-stream-kafka_2.12:0.17'

    testCompile 'org.scalatest:scalatest_2.12:3.0.4'
    testCompile 'org.scalamock:scalamock_2.12:4.0.0'
    testCompile 'com.typesafe.akka:akka-testkit_2.12:2.5.6'
    testCompile 'com.typesafe.akka:akka-http-testkit_2.12:10.0.10'
    testCompile 'com.github.dnvriend:akka-persistence-inmemory_2.12:2.5.1.1'
}

jar {
    manifest.attributes ([
        'Main-Class': mainClass,
        'Implementation-Title': project.name,
        'Implementation-Version': version,
        'Class-Path': (configurations.runtime + configurations.compile).collect { "libs/" + it.getName() }.join(' ')
    ])
}

task buildDistribution(type: Copy, dependsOn: [jar]) {
    into project.distributionDir
    from('src/launcher/keyscore-agent')
    from(jar.outputs) {
        rename("${project.name}-${version}.jar", "${project.name}.jar")
    }
    from(configurations.runtime) {
        into 'libs'
    }

}

task buildDockerfile(type: Dockerfile) {
    destFile = project.file("$buildDir/tmp/Dockerfile")
    from 'java:openjdk-8-jdk-alpine'
    instruction { "COPY build/distribution/ /opt/keyscore-agent/" }
    instruction { "CMD /opt/keyscore-agent/keyscore-agent" }
    instruction { 'EXPOSE 4711' }
}

task buildDockerImage(type: DockerBuildImage, dependsOn: [buildDistribution, buildDockerfile]) {
    inputDir = projectDir
    dockerFile = buildDockerfile.destFile
    tag = 'logbee/keyscore-agent:latest'
}
